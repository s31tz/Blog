#!/usr/bin/env perl

use strict;
use warnings;
use utf8;

use FindBin qw/$Bin/;;
use lib "$Bin/../lib/perl5";
use open OUT=>':locale';
use Blog::Base::Prty::Sdoc::Document;
use Blog::Base::Prty::Option;
use Blog::Base::R1::Help;
use Blog::Base::Prty::ApplicationPaths;
use Blog::Base::Prty::Config;
use Blog::Base::Prty::Database::Connection;
use Blog::Base::Prty::Process;
use Blog::Base::Prty::Path;

# -----------------------------------------------------------------------------

=encoding utf8

=head1 NAME

blog-update - Aktualisiere Serendipity Blog-Datenbank

=head1 USAGE

blog-update [OPTIONS]

=head1 OPTIONS

=over 4

=item --dry-run

Zeige die Liste der Artikel, die zu aktualisieren sind, aber
aktualisiere sie nicht.

=item --verbose

Zeige die ausgeführten SQL-Statements an.

=item --help

Diese Hilfe.

=back

=head1 DESCRIPTION

Das Programm prüft, welche .sdoc-Dateien geändert wurden
(deren mtime also jünger ist als das last_modified-Datum des
Artikels auf der Serendipity-Datenbank) wandelt die .sdoc-Datei
nach HTML und aktualisiert die betreffenden Artikel auf der Datenbank.

=head1 EXIT CODES

=over 4

=item Z<>0

Es wurden Artikel aktualisiert

=item Z<>1

Es wurden keine Artikel aktualisiert

=back

=head1 AUTHOR

Frank Seitz, L<http://fseitz.de/>

=cut

# -----------------------------------------------------------------------------

# Hilfsfunktion: Wandele Sdoc-Datei in Serendipty-Daten

my $convertFile = sub {
    my $file = shift;

    my $sdoc =  Blog::Base::Prty::Sdoc::Document->new("$file",-utf8=>1);
    my $html = Encode::decode('utf-8',$sdoc->dump('ehtml'));
    $html =~ s|<h1.*?>(.*?)</h1>\n+||;
    my $title = $1;

    # Relative Bild-URLs für XING expandieren
    $html =~ s|"IMGDIR/|"http://fseitz.de/blog/uploads/|g;

    return ($title,$html);
};

# Optionen

my $dryRun = 0;
my $verbose = 0;
my $help = 0;

Blog::Base::Prty::Option->extract(\@ARGV,
    -dryRun=>\$dryRun,
    -verbose=>\$verbose,
    -help=>\$help,
);
if ($help) {
    Blog::Base::R1::Help->exit;
}
elsif (@ARGV) {
    Blog::Base::R1::Help->exit(10,'ERROR: Too many arguments');
}

# Konfiguration

my $app = Blog::Base::Prty::ApplicationPaths->new;
my $cfg = Blog::Base::Prty::Config->new($app->etcDir('blog.conf'));

# Datenbankverbindung aufbauen
my $db = Blog::Base::Prty::Database::Connection->new($cfg->get('Udl'),
    -utf8=>1,
    -log=>$verbose,
);

# Artikelinformation von der Datenbank lesen

my $entT = $db->select(
    -select=>qw/id last_modified/,
    -from=>'serendipity_entries',
);
my %idIdx = $entT->index('id');

# Ins Artikelverzeichnis wechseln

my $articleDir = $app->homeDir('article');
Blog::Base::Prty::Process->cwd($articleDir);

# Artikeldateien lesen

my $n = 0; # Anzahl aktualisierter Artikel
for my $file (Blog::Base::Prty::Path->glob("*.sdoc")) {
    my $mtime = Blog::Base::Prty::Path->mtime($file);
    my $text = Encode::decode('utf8',Blog::Base::Prty::Path->read($file));

    my ($id) = $text =~ /^# (?:id=|Id: )(\d+)/m;
    if (!$id) {
        # Neuer Eintrag: Daten für Serendipity ausgeben
        warn "WARNING: Datei enthält keine id: $file\n";

        my ($title,$html) = $convertFile->($file);
        print "Title: $title\n\nHTML:\n$html";

        next;
    }

    my $ent = $idIdx{$id};
    if (!$ent) {
        warn "WARNING: Artikel-Id auf der Datenbank nicht gefunden: $id\n";
        next;
    }

    if ($mtime > $ent->last_modified) {
        # Existierender Eintrag: Blog-DB aktualisieren
        printf "Aktualisiere: %s\n",$file;

        if (!$dryRun) {
            # Draft-Eigenschft ermitteln

            my $isDraft = 'f';
            if ($text =~ /^# (?:Draft: )(.+)/m) {
                if ($1 eq 'yes') {
                    $isDraft = 't';
                }
            }

            # Artikel aktualisieren

            my ($title,$html) = $convertFile->($file);
            $db->update('serendipity_entries',
                title=>Encode::encode('iso-8859-1',$title),
                body=>Encode::encode('iso-8859-1',$html),
                last_modified=>$mtime,
                isdraft=>$isDraft,
                -where,id=>$id,
            );
            $n++;

            if ($isDraft eq 'f') {
                # Kategorien und Kategoriezuordnungen aktualisieren
                # Im Blog-Artikel: "# Categories: CATEGORY1, CATEGORY2, ...

                $db->delete('serendipity_entrycat',entryid=>$ent->id);
                if (my ($categories) = $text =~ /^# Categories: (.*)/m) {
                    for my $category (split /,\s*/,$categories) {
                        my $cat = $db->lookup(
                            -from=>'serendipity_category',
                            -where,category_name=>$category,
                            -new=>1,
                        );
                        if ($cat->rowStatus eq 'I') {
                            $cat->set(
                                categoryid=>$db->nextValue(
                                    'serendipity_category_categoryid_seq'),
                                category_name=>$category,
                            );
                            $db->insert('serendipity_category',$cat);
                            print "Neue Kategorie: $category\n";
                        }
                        my $eca = $db->lookup(
                            -from=>'serendipity_entrycat',
                            -where,entryid=>$ent->id,
                                categoryid=>$cat->categoryid,
                            -new=>1,
                        );
                        if ($eca->rowStatus eq 'I') {
                            $eca->set(
                                entryid=>$ent->id,
                                categoryid=>$cat->categoryid,
                            );
                            $db->insert('serendipity_entrycat',$eca);
                        }
                    }
                }
            }
        }
    }
}
$db->commit;

# Wenn Artikel aktualisiert wurden, terminieren wir mit 0, sonst 1
exit $n? 0: 1;

# eof

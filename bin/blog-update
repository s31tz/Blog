#!/usr/bin/env perl

use v5.10;
use strict;
use warnings;
use utf8;

use FindBin qw/$Bin/;;
use lib "$Bin/../lib/perl5";
use open OUT=>':locale';
use Blog::Base::Quiq::Path;
use Blog::Base::Quiq::ApplicationPaths;
use Blog::Base::Quiq::Config;
use Blog::Base::Quiq::Process;
use Sdoc2::Document;
use Blog::Base::Quiq::Option;
use Blog::Base::R1::Help;
use Blog::Base::Quiq::Database::Connection;
use Blog::Base::Quiq::Shell;

# -----------------------------------------------------------------------------

=encoding utf8

=head1 NAME

blog-update - Aktualisiere Serendipity Blog-Datenbank

=head1 USAGE

blog-update [OPTIONS]

=head1 OPTIONS

=over 4

=item --dry-run

Zeige die Liste der Artikel, die zu aktualisieren sind, aber
aktualisiere sie nicht.

=item --verbose

Zeige die ausgeführten SQL-Statements an.

=item --help

Diese Hilfe.

=back

=head1 DESCRIPTION

Das Programm prüft, welche .sdoc-Dateien geändert wurden
(deren mtime also jünger ist als das last_modified-Datum des
Artikels auf der Serendipity-Datenbank) wandelt die .sdoc-Datei
nach HTML und aktualisiert die betreffenden Artikel auf der Datenbank.

=head1 EXIT CODES

=over 4

=item Z<>0

Es wurden Artikel aktualisiert

=item Z<>1

Es wurden keine Artikel aktualisiert

=back

=head1 AUTHOR

Frank Seitz

=cut

# -----------------------------------------------------------------------------

# Allgemeines

my $p = Blog::Base::Quiq::Path->new;

# Konfiguration

my $app = Blog::Base::Quiq::ApplicationPaths->new;
my $cfg = Blog::Base::Quiq::Config->new($app->etcDir('blog.conf'));

# Artikelverzeichnis ermitteln
my $articleDir = $app->homeDir('article');

# Hilfsfunktion: Wandele Sdoc-Datei in Serendipty-Daten

my $convertFile = sub {
    my ($articleDir,$file) = @_;

    my $dir = Blog::Base::Quiq::Path->dir($file);
    Blog::Base::Quiq::Process->cwd($dir);
    my $sdoc =  Sdoc2::Document->new("$file",-utf8=>1);
    Blog::Base::Quiq::Process->cwd($articleDir);
    my $html = Encode::decode('utf-8',$sdoc->dump('ehtml'));
    $html =~ s|<h1.*?>(.*?)</h1>\n+||;
    my $title = $1;

    # Relative Bild-URLs für XING expandieren
    $html =~ s|"IMGDIR/|"http://fseitz.de/blog/uploads/|g;

    return ($title,$html);
};

# Optionen

my $dryRun = 0;
my $verbose = 0;
my $help = 0;

Blog::Base::Quiq::Option->extract(\@ARGV,
    -dryRun=>\$dryRun,
    -verbose=>\$verbose,
    -help=>\$help,
);
if ($help) {
    Blog::Base::R1::Help->exit;
}
elsif (@ARGV) {
    Blog::Base::R1::Help->exit(10,'ERROR: Too many arguments');
}

# Datenbankverbindung aufbauen
my $db = Blog::Base::Quiq::Database::Connection->new($cfg->get('Udl'),
    -utf8=>1,
    -log=>$verbose,
);

# Artikelinformation von der Datenbank lesen

my $entT = $db->select(
    -select=>qw/id last_modified/,
    -from=>'serendipity_entries',
);
my %idIdx = $entT->index('id');

# Artikeldateien lesen

my @files;
for my $articleDir ($articleDir,"$articleDir-new") {
    push @files,$p->find($articleDir,-type=>'f',-pattern=>'\.sdoc$');
}

my $n = 0; # Anzahl aktualisierter Artikel
for my $file (@files) {
    my $text = Encode::decode('utf8',$p->read($file));

    my ($id) = $text =~ /^# (?:id=|Id: )(\d+)/m;
    if (!$id) {
        # Neuer Eintrag: Daten für Serendipity ausgeben
        warn "WARNING: Datei enthält keine d: $file\n";

        my ($title,$html) = $convertFile->($articleDir,$file);
        print "Title: $title\n\nHTML:\n$html";

        next;
    }

    my $ent = $idIdx{$id};
    if (!$ent) {
        warn "WARNING: Artikel-Id auf der Datenbank nicht gefunden: $id\n";
        next;
    }

    # Jüngste Änderung an der oder den Quelldateien

    my $dir = $p->dir($file);
    my $mtime = $dir eq $articleDir? $p->mtime($file): $p->mtimeDir($dir);

    # Jüngste Änderung am Generierungsprogramm (sofern vorhanden)

    my $ext = $p->extension($file) eq 'sdoc3'? 'srun3': 'srun';
    my $program = $p->basePath($file).".$ext";
    if (-e $program) {
        my $t = Blog::Base::Quiq::Shell->exec("$program --mtime",-capture=>'stdout');
        chomp $t;
        if ($t > $mtime) {
            $mtime = $t;
        }
    }

    if ($mtime > $ent->last_modified) {
        # Existierender Eintrag: Blog-DB aktualisieren
        printf "Aktualisiere: %s\n",$file;

        if (!$dryRun) {
            # Draft-Eigenschaft ermitteln

            my $isDraft = 'f';
            if ($text =~ /^# (?:Draft: )(.+)/m) {
                if ($1 eq 'yes') {
                    $isDraft = 't';
                }
            }

            # Artikel aktualisieren

            my ($title,$html) = $convertFile->($articleDir,$file);
            $db->update('serendipity_entries',
                title=>Encode::encode('iso-8859-1',$title),
                body=>Encode::encode('iso-8859-1',$html),
                last_modified=>time,
                isdraft=>$isDraft,
                -where,id=>$id,
            );
            $n++;

            if ($isDraft eq 'f') {
                # Kategorien und Kategoriezuordnungen aktualisieren
                # Im Blog-Artikel: "# Categories: CATEGORY1, CATEGORY2, ...

                $db->delete('serendipity_entrycat',entryid=>$ent->id);
                if (my ($categories) = $text =~ /^# Categories: (.*)/m) {
                    for my $category (split /,\s*/,$categories) {
                        my $cat = $db->lookup(
                            -from=>'serendipity_category',
                            -where,category_name=>$category,
                            -new=>1,
                        );
                        if ($cat->rowStatus eq 'I') {
                            $cat->set(
                                categoryid=>$db->nextValue(
                                    'serendipity_category_categoryid_seq'),
                                category_name=>$category,
                            );
                            $db->insert('serendipity_category',$cat);
                            print "Neue Kategorie: $category\n";
                        }
                        my $eca = $db->lookup(
                            -from=>'serendipity_entrycat',
                            -where,entryid=>$ent->id,
                                categoryid=>$cat->categoryid,
                            -new=>1,
                        );
                        if ($eca->rowStatus eq 'I') {
                            $eca->set(
                                entryid=>$ent->id,
                                categoryid=>$cat->categoryid,
                            );
                            $db->insert('serendipity_entrycat',$eca);
                        }
                    }
                }
            }
        }
    }
}
$db->commit;

# Wenn Artikel aktualisiert wurden, terminieren wir mit 0, sonst mit 1
exit($n? 0: 1);

# eof

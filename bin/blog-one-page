#!/usr/bin/env perl

use v5.10;
use strict;
use warnings;

use FindBin qw/$Bin/;
use lib "$Bin/../lib/perl5";
use open OUT=>':locale';
use POSIX ();
use Blog::Base::Quiq::Option;
use Blog::Base::R1::Help;
use Blog::Base::Quiq::ApplicationPaths;
use Blog::Base::Quiq::Config;
use Blog::Base::Quiq::Process;
use Blog::Base::Quiq::Path;
use Sdoc2::Document;
use Blog::Base::Quiq::Html::Tag;

# -----------------------------------------------------------------------------

=head1 NAME

blog-one-page - Erzeuge eine HTML-Seite mit allen Artikeln

=head1 USAGE

blog-one-page [OPTIONS]

=head1 OPTIONS

=over 4

=item --embed=BOOL (Default: 1)

Bette Stylesheets und Bilder in HTML-Code ein.

=item --format=FORMAT (Default: 'html')

Format der generierten Datei: 'html' oder 'sdoc'.

=item --help

Diese Hilfe.

=back

=head1 AUTHOR

Frank Seitz

=cut

# -----------------------------------------------------------------------------

# Optionen

my $embed = 1;
my $format = 'html';
my $verbose = 0;
my $help = 0;

Blog::Base::Quiq::Option->extract(\@ARGV,
    -embed=>\$embed,
    -format=>\$format,
    -verbose=>\$verbose,
    -help=>\$help,
);
if ($help) {
    Blog::Base::R1::Help->exit;
}
elsif (@ARGV) {
    Blog::Base::R1::Help->exit(10,'ERROR: Too many arguments');
}

# Konfiguration

my $app = Blog::Base::Quiq::ApplicationPaths->new;
my $cfg = Blog::Base::Quiq::Config->new($app->etcDir('blog.conf'));

my $homeDir = $app->homeDir;
my $articleDir = $app->homeDir('article');
my $imageDir = $app->homeDir('image');
my $cssDir = $app->homeDir('css');

# Ins Artikelverzeichnis wechseln
Blog::Base::Quiq::Process->cwd($articleDir);
# Blog::Base::Quiq::Process->cwd($homeDir);

# Artikeldateien zusammenfügen

my $sdoc = '';
my $maxMtime = 0;
for my $file (reverse Blog::Base::Quiq::Path->glob("*.sdoc")) {
# for my $file (reverse Blog::Base::Quiq::Path->find('.',-pattern=>qr|^article-new/.*\.sdoc$|)) {
    my ($num) = $file =~ m|(\d+)|;
    $num += 0;
    my $text = Blog::Base::Quiq::Path->read($file);

    # Drafts übergehen wir

    if ($text =~ /^# (?:Draft: )(.+)/m) {
        if ($1 eq 'yes') {
            next;
        }
    }

    # Zeitpunkt der jüngsten Änderung

    my $mtime = Blog::Base::Quiq::Path->mtime($file);
    if ($mtime > $maxMtime) {
        $maxMtime = $mtime;
    }

    # Titel des Artikels bestimmen

    my ($title) = $text =~ /title="(.*?)"/;
    $title = sprintf "%03d. $title",$num;

    # Text manipulieren

    $text =~ s/.*^%Document:.*?\n\n//sm; # Anfang wegschneiden
    $text =~ s/^(=+)/=$1/mg;         # Abschnitte eine Ebene tiefer
    $text =~ s/IMGDIR/$imageDir/g;   # Bildverzeichnis eintragen
    $text =~ s/^# eof\n//m;          # # eof entfernen
    $text =~ s/\s+$//;               # Whitespace am Ende entfernen

    if ($sdoc) {
        $sdoc .= "\n\n";
    }
    $sdoc .= "= $title\n\n$text";
}

# Stylesheets

my $embedImages = $embed? 1: 0;
my @styleSheets = qw/blog-style.css blog-highlight.css/;
for my $styleSheet (@styleSheets) {
    $styleSheet = "$cssDir/$styleSheet";
    if ($embed) {
        $styleSheet = "inline:$styleSheet";
    }
}    
my $styleSheets = join ' ',@styleSheets;

# Dokument generieren

my $year = POSIX::strftime('%Y',localtime);
my $date = POSIX::strftime('%Y-%m-%d',localtime);
my $mtime = POSIX::strftime('%Y-%m-%d %H:%M:%S',localtime($maxMtime));

# ACHTUNG: \xc2\xa9 kann nicht durch (C) ersetzt werden!

$sdoc = <<"__EOT__";
%Document:
  title="Frank Seitz - Developer Logbuch"
  styleSheet="$styleSheets"
  embedImages=$embedImages
  generateAnchors=0
  utf8=1

Copyright (C) 2010-$year Frank Seitz, U{http://fseitz.de/}, Last update: $mtime

%TableOfContents:
  maxDepth=1

$sdoc

# eof
__EOT__

if ($format eq 'sdoc') {
    print Encode::decode('utf-8',$sdoc);
}
else {
    my $html = Sdoc2::Document->new(\$sdoc,-utf8=>1)->dump('html');
    my $h = Blog::Base::Quiq::Html::Tag->new;
    my @jsLibs = (
        'https://code.jquery.com/jquery-latest.min.js',
        'https://cdn.plot.ly/plotly-latest.min.js',
    );
    my $tmp = '';
    for my $jsLib (@jsLibs) {
        $tmp .= '  '.$h->tag('script',src=>$jsLib);
    }
    $html =~ s|(</head>)|$tmp$1|;
    print Encode::decode('utf-8',$html);
}

# eof
